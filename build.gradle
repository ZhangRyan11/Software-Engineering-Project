plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.4'
    id 'eclipse'
}

eclipse.project.name = 'Cs Get Degrees'

repositories {
    mavenCentral()
}

def grpcVersion = '1.59.1'
def protobufVersion = '3.25.1'
def protocVersion = protobufVersion

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-services:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-netty:${grpcVersion}"
    compileOnly "org.apache.tomcat:annotations-api:6.0.53"
    
    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    testImplementation "io.grpc:grpc-inprocess:${grpcVersion}"
    testImplementation('junit:junit:4.12')
    testImplementation('org.junit.jupiter:junit-jupiter:5.10.2')
    testImplementation('org.mockito:mockito-core:5.10.0')
}

sourceSets {
    main {
        proto {
            srcDir 'src/main/proto'
        }
        java {
            srcDirs 'src',
                    'build/generated/source/proto/main/grpc',
                    'build/generated/source/proto/main/java'
        }
    }
    test {
        java {
            srcDirs = ['test']
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protocVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
    generatedFilesBaseDir = "$projectDir/build/generated"
}

test {
    useJUnitPlatform()
    filter {
        excludeTestsMatching "project.annotations.Assignment*"
        setFailOnNoMatchingTests(false)
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}
